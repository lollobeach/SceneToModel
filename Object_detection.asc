# TODO: remove once the script is ultimated
OBJECT_DETECTION

# Object Detection from local path, imports a JSON file and create its IoTScenario representation
PROCEDURE global OBJECT_DETECTION
{

    CC "Modeling" GET_ACT_MODEL
    SETL actModel:(modelid)

    CC "Core" GET_ALL_OBJS modelid:(actModel)

    ##########      1. MODEL INITIALIZATION     ##########

    # If no model has been selected, create a new model and place objects read from the JSON file,
    # or do the same thing if there's a model selected but there are objects in it.
    IF(actModel=-1 OR objids.length != 0) {
        # Select a target model group
        CC "CoreUI" MODEL_SELECT_BOX without-models mgroup-sel title:"Select one target modelgroup"
        IF (endbutton != "ok") {
            SETL targetID:-1
            EXIT
        }
        SETL a_mgroupids:(mgroupids)

        # Let the user enter a new modelname
        CC "AdoScript" EDITFIELD title:("New model name") caption:("Modelname:")
        IF (ecode != 0) {
            SETL targetID:-1
            EXIT
        }
        SETL a_name:(text)

        CC "Core" CREATE_MODEL modeltype:("IoT Scenario") modelname:(a_name) version:"" mgroups:(a_mgroupids)
        
        IF (ecode = 0) {
            SETL a_dstmodelid:(modelid)
        } ELSIF (ecode = 40) {
            CC "AdoScript" ERRORBOX ("A model with the same name already exists") ok
        } ELSE {
            CC "AdoScript" ERRORBOX ("An error occured creating the new model: " + errtext) ok
        }
   
        # Open new model in modeling
        CC "Modeling" OPEN modelids:(STR a_dstmodelid)
        CC "Modeling" SET_VIEW_MODE modelid:(a_dstmodelid) mode-name:"PSM"
        CC "Core" SET_ATTR_VAL objid:(a_dstmodelid) attrname:"Model Type" val:("PSM")
        CC "Core" SET_ATTR_VAL objid:(a_dstmodelid) attrname:"Platform" val:("Object_detection")

        # Get all objects from new model
        CC "Core" GET_ALL_OBJS modelid:(a_dstmodelid)
        IF (ecode != 0) {
            CC "AdoScript" ERRORBOX "Something went very wrong here (e.g. the wrong model id have been received)!\n"
            CC "Modeling" CLOSE modelid:(a_dstmodelid) quiet
            CC "Core" DELETE_MODEL modelid:(a_dstmodelid)
            SETL targetID:-1
            EXIT
        }

        # Add PSM attributes to objects
        FOR obj in:(objids) {
            SET_SPECIFICATIONS objID:(obj) specs:(specs)
        }

   
        # Save model
        CC "Core" SAVE_MODEL modelid:(a_dstmodelid)
        SET actModel:(a_dstmodelid)     #CC "Core" DISCARD_MODEL modelid:(a_dstmodelid)
    }


    ##########      2. Object detection model creation      ########## 

    # READ .JSON FILE: create elements and assign them their values (read from the json file)
    # Select the .json file path
    CC "AdoScript" FILE_DIALOG open
        filter1:"JSON Files" type1:"*.json" default-ext:"json"
    SET filePath:(path)
    IF (filePath = "") {
        CC "AdoScript" ERRORBOX ("No file selected. Process aborted.")
        EXIT
    }

    # Read JSON file
    CC "AdoScript" FREAD file:(filePath)
    IF (ecode != 0) {
        CC "AdoScript" ERRORBOX ("The file is not readable!") ok
        EXIT
    }

    # Convert file content to JSON
    SETL specs:(fromJson(text))
    SET objectsArray:(specs["predictions"])

    GET_SPECIFICATIONS platform:("Object_detection") specs:specs        # could be dynamic adding platform as a variable (to be asked to the user)
    SET specifications:(specs)

    # create elements from the json
	CREATE_NEW_OBJECTS modelid:actModel objects:objectsArray

    ##########      Solution discarded: 3. Possible TCP connection in which exchange SOAP messages (maybe images)      ########## 
    # If started, ADOxx opens a port where SOAP messages can be sent. Over this endpoint AdoScript can be sent, which is then executed in the current ADOxx instance.
    # Problem can occur if the webservice server executes AdoScript at the same time, the user interface is changed through user interaction.
    # Therefore, it is recommend to block the user interaction, for example thorough the MSGWIN command.
    # CC "AdoScript" SERVICE	start port:8080 backlog:1000 logformat:long output:statusbar   # problema: chiede autorizzazione all'OS :/
    
    # TODO remove this
    #CC "Core" GET_ATTR_VAL objid:(actModel) attrname:"Load image"
    #IF(val = "") {
    #    CC "AdoScript" FILE_DIALOG open
    #        filter1:"JPEG files" type1:"*.jpg"
    #        filter2:"PNG files" type2:"*.png"
    #        filter3:"BMP files" type3:"*.bmp"
    #    CC "Core" SET_ATTR_VAL objid: (actModel) attrname: "Load image" val: (path)
    #}
    #CC "Core" GET_ATTR_VAL objid:(actModel) attrname:"Load image"
    #CC "AdoScript" FREAD file:(val) binary:false base64:true           # does not work
    #CC "AdoScript" INFOBOX(text)
    #CC "AdoScript" INFOBOX(fromJson(text))

    #BITMAPINFO (val) # read background image info --> CAN BE DONE ONLY IN GRAPHREP SECTIONS... :(

}





# Create new object and set new name (and Name) value
PROCEDURE global CREATE_NEW_OBJECTS 
modelid:reference
objects:reference
{        
    EXTRACT_CLASSIDS    # always usefull

    FOR i from:0 to:(LEN objects-1) {
		SETL objectType:(objects[i] SUB "iot_device")

        # Capabilities could be defined in future
        #SETL capability1:(objects[i] SUB "capability1")
		#SETL capability2:(objects[i] SUB "capability2")
		#SETL specification:(objects[i] SUB "Nspecification")

        # Position is represented in pixels while in ADOxx it is used the CM (centimeters) representation
		SETL xPosition:(objects[i] SUB "x")
        SETL yPosition:(objects[i] SUB "y")


        IF( objectType = "person") {
	        CC "Core" CREATE_OBJ modelid:(modelid) classid:(personClassId)
        } ELSIF(objectType ="gateway") {
	        CC "Core" CREATE_OBJ modelid:(modelid) classid:(gatewayClassId)
        } ELSIF(objectType ="temperature") {
	        CC "Core" CREATE_OBJ modelid:(modelid) classid:(thermometerClassId)
        } ELSIF(objectType ="photocell") {
	        CC "Core" CREATE_OBJ modelid:(modelid) classid:(photocellClassId)
        } ELSIF(objectType ="airQuality") {
	        CC "Core" CREATE_OBJ modelid:(modelid) classid:(airQualityClassId)
        }
    	SETL newObjId:(objid)
        SET_SPECIFICATIONS objID: (STR newObjId) specs: (specs)
	    CC "Core" SET_ATTR_VAL objid:(newObjId) attrname:("name") val:(objectType + " - " + STR i)
    	CC "Core" SET_ATTR_VAL objid:(newObjId) attrname:("Name") val:(objectType + " - " + STR i)


        SETL x:(CM(xPosition/20))   # original values stands for pixel (e.g.: 397 etc... so it tries to represent it correctly in the model)
        SETL y:(CM(yPosition/20))

        CC "Modeling" SET_OBJ_POS objid:(newObjId) x:(x) y:(y)


        # SPECIFICATION TABLE ATTRIBUTES & PSM_TYPE
		#CC "Core" GET_ATTR_ID classid:(deviceClassId) attrname:("specification")

		#CC "Core" GET_REC_ATTR_ROW_ID objid:(newObjId) attrid:(attrid) index:1
        #CC "Core" SET_ATTR_VAL objid:(rowid) attrname:("Value") val:(capability1)

        #CC "Core" GET_REC_ATTR_ROW_ID objid:(newObjId) attrid:(attrid) index:2
        #CC "Core" SET_ATTR_VAL objid:(rowid) attrname:("Value") val:(capability2)

		#CC "Core" GET_REC_ATTR_ROW_ID objid:(newObjId) attrid:(attrid) index:3
        #CC "Core" SET_ATTR_VAL objid:(rowid) attrname:("Value") val:(specification)
    }
}

# Call this procedure once to avoid repeating n times the GET_CLASS_ID command call
PROCEDURE global EXTRACT_CLASSIDS 
{
	# Classes
    CC "Core" GET_CLASS_ID classname:("person")
	SETG personClassId:(classid)
    CC "Core" GET_CLASS_ID classname:("airQuality")
	SETG airQualityClassId:(classid)
    CC "Core" GET_CLASS_ID classname:("thermometer")
	SETG thermometerClassId:(classid)
    CC "Core" GET_CLASS_ID classname:("gateway")
	SETG gatewayClassId:(classid)
    CC "Core" GET_CLASS_ID classname:("photocell")
    SETG photocellClassId:(classid)

	# Relation classes
	CC "Core" GET_CLASS_ID classname:("objDetLink")
	SETG objDetLinkClassId:(classid)
}


PROCEDURE global SET_SPECIFICATIONS
objID:string    # Object's id
specs:map       # Platform's specifications
{
    # Get object's classname
    CC "Core" GET_CLASS_ID objid:(VAL objID)
    CC "Core" GET_CLASS_NAME classid:(classid)
    SETL class:(specs[classname])
    
    IF (type(class) = "map") {
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("specification")
        IF (attrid != -1 AND type(class["specs"]) = "map") {
            SETL attrs:(mapKeysArray(class["specs"]))
            FOR j from:0 to:(attrs.length-1) {
                # Add attribute to specification table
                SETL attrClass:(class["specs"][attrs[j]])
                CC "Core" ADD_REC_ROW objid:(VAL objID) attrid:(attrid)
                CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:(attrs[j])
                IF (type(attrClass) = "map") {
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:(attrClass["type"])
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:(attrClass["description"])
                }
                IF (ecode != 0) {
                    CC "AdoScript" ERRORBOX ("Error adding a row to attribute \"" + (attrs[j]) + "\"")
                    EXIT
                }
            }
        }
        # Get all outcoming connectors
        IF (type(class["config"]) != "undefined") {
            CC "Core" GET_CONNECTORS objid:(VAL objID) out
            FOR connector in:(objids) {
                # Add connector types
                SETL types:(lam_toList(class["output"], "~"))
                CC "Core" SET_ATTR_VAL objid:(VAL connector) attrname:"available_types" val:(types)
            }
        }
    } ELSIF (type(class) = "array") {
        SETL types:(mapKeysList(class[0]))
        CC "Core" SET_ATTR_VAL objid:(VAL objID) attrname:("available_types") val:(types)
    }
}

## Procedure to get platform specifications
PROCEDURE global GET_SPECIFICATIONS 
platform:string     # Platform's name
specs:reference     # Platform's specifications (output)
{
    # Select right file
    SETL filePath:(-1)
    IF (platform = "Object_detection") {
        SETL filePath:("db:\\Object_detection.json")
    } 

    # Read JSON file
    CC "AdoScript" FREAD file:(filePath)
    IF (ecode != 0) {
        CC "AdoScript" ERRORBOX ("The file is not readable!") ok
        EXIT
    }

    # Convert file content to JSON
    SETL specs:(fromJson(text))
}